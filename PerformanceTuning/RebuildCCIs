-- Find CCI Tables where compressed rowgroups have fewer than 100k records for more than 10% of the total compressed rowgroups in the CCI
SELECT 
	AggTable.[Schema_Name]
	,AggTable.Logical_Table_Name
	--,AggTable.[Total_Compressed_Rowgroup_Count]
	--,COUNT(1) as [Small_Rowgroups]
	,'ALTER INDEX ALL ON ['+[Schema_Name]+'].['+Logical_Table_Name+'] REBUILD;' as Command
FROM 
(
	select   
		sm.name as [Schema_Name]
	,	tb.[name] AS [Logical_Table_Name]
	,	COUNT(1) AS [Total_Compressed_Rowgroup_Count]
	FROM    sys.[schemas] sm
	JOIN    sys.[tables] tb               ON  sm.[schema_id]          = tb.[schema_id]
	JOIN    sys.[pdw_table_mappings] mp   ON  tb.[object_id]          = mp.[object_id]
	JOIN    sys.[pdw_nodes_tables] nt     ON  nt.[name]               = mp.[physical_name]
	JOIN    sys.[dm_pdw_nodes_db_column_store_row_group_physical_stats] rg      ON  rg.[object_id]     = nt.[object_id]
				AND rg.[pdw_node_id]   = nt.[pdw_node_id]
				AND rg.[distribution_id]    = nt.[distribution_id]
	WHERE 	rg.state_desc = 'COMPRESSED'
	GROUP BY 
		sm.name
		,tb.[name]
) AggTable
JOIN sys.[schemas] sm1 ON AggTable.[Schema_Name] = sm1.[Name]
JOIN    sys.[tables] tb1               ON  sm1.[schema_id]          = tb1.[schema_id] AND tb1.name = AggTable.Logical_Table_Name
JOIN    sys.[pdw_table_mappings] mp1   ON  tb1.[object_id]          = mp1.[object_id]
JOIN    sys.[pdw_nodes_tables] nt1     ON  nt1.[name]               = mp1.[physical_name]
JOIN    sys.[dm_pdw_nodes_db_column_store_row_group_physical_stats] rg1      ON  rg1.[object_id]     = nt1.[object_id]
				AND rg1.[pdw_node_id]   = nt1.[pdw_node_id]
				AND rg1.[distribution_id]    = nt1.[distribution_id]
WHERE 
	rg1.total_rows < 100000
	and rg1.state_desc = 'COMPRESSED'
GROUP BY 
	AggTable.[Schema_Name]
	,AggTable.Logical_Table_Name
	,AggTable.[Total_Compressed_Rowgroup_Count]
HAVING COUNT(1) > AggTable.[Total_Compressed_Rowgroup_Count]*.10
UNION
-- CCI Tables with large Delete Bitmap
select   sm.name as [Schema_Name]
		,tb.[name]                    AS [Logical_Table_Name]
		--,sum(rg.[total_rows])         AS [total_CCI_rows]
		--,sum(deleted_rows)			  AS [deleted_rows]
		--,sum(rg.[total_rows])-sum(deleted_rows) as [Non-Deleted_Rows]
		--,round((sum(deleted_rows)*100/(sum(rg.[total_rows]))),0) as PercentDeleted
		,'ALTER INDEX ALL ON ['+sm.name+'].['+tb.name+'] REBUILD;' as Command
FROM    sys.[schemas] sm
JOIN    sys.[tables] tb               ON  sm.[schema_id]          = tb.[schema_id]
JOIN    sys.[pdw_table_mappings] mp   ON  tb.[object_id]          = mp.[object_id]
JOIN    sys.[pdw_nodes_tables] nt     ON  nt.[name]               = mp.[physical_name]
JOIN    sys.[dm_pdw_nodes_db_column_store_row_group_physical_stats] rg      ON  rg.[object_id]     = nt.[object_id]
			AND rg.[pdw_node_id]   = nt.[pdw_node_id]
            AND rg.[distribution_id]    = nt.[distribution_id]
GROUP BY sm.name, tb.name, rg.[object_id], index_id
HAVING CASE WHEN sum(rg.[total_rows]) = 0 THEN 0 ELSE round((sum(deleted_rows)*100/(sum(rg.[total_rows]))),0) END >=20 -- deleted records/nondeleted records is greater than 20%
UNION
-- CCI Tables with large number of open rowgroups
select   
	sm.name as [Schema_Name]
	,tb.[name] AS [Logical_Table_Name]
	--,count(1) as Open_Rowgroup_Count
	,'ALTER INDEX ALL ON ['+sm.name+'].['+tb.name+'] REBUILD;' as Command
FROM    sys.[schemas] sm
JOIN    sys.[tables] tb               ON  sm.[schema_id]          = tb.[schema_id]
JOIN    sys.[pdw_table_mappings] mp   ON  tb.[object_id]          = mp.[object_id]
JOIN    sys.[pdw_nodes_tables] nt     ON  nt.[name]               = mp.[physical_name]
JOIN    sys.[dm_pdw_nodes_db_column_store_row_group_physical_stats] rg      ON  rg.[object_id]     = nt.[object_id]
			AND rg.[pdw_node_id]   = nt.[pdw_node_id]
            AND rg.[distribution_id]    = nt.[distribution_id]
WHERE state_desc = 'OPEN'
GROUP BY 
	 sm.name
	,tb.[name]
	,rg.object_ID
HAVING COUNT(1) > 90
UNION
-- Small CCI Tables that should be considered for clustered index instead
SELECT   sm.name as [Schema_Name]
		,tb.[name] AS [Logical_Table_Name]
		--,sum(rg.[total_rows])         AS [total_CCI_rows]
		,'--Create Table ['+sm.name+'].['+tb.name+'_New] WITH (CLUSTERED INDEX(MyColumn), distribution=hash(MyColumn)) as SELECT * FROM ['+sm.name+'].['+tb.name+'];--RENAME, RENAME' as Command
FROM    sys.[schemas] sm
JOIN    sys.[tables] tb               ON  sm.[schema_id]          = tb.[schema_id]
JOIN    sys.[pdw_table_mappings] mp   ON  tb.[object_id]          = mp.[object_id]
JOIN    sys.[pdw_nodes_tables] nt     ON  nt.[name]               = mp.[physical_name]
JOIN    sys.[dm_pdw_nodes_db_column_store_row_group_physical_stats] rg      ON  rg.[object_id]     = nt.[object_id]
			AND rg.[pdw_node_id]   = nt.[pdw_node_id]
            AND rg.[distribution_id]    = nt.[distribution_id]
GROUP BY sm.name, tb.name, rg.[object_id], index_id
HAVING sum(rg.[total_rows]) < 60000000 -- Show only CCI tables with less than 60m records
ORDER BY Command
